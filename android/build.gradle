buildscript {
  // Buildscript is evaluated before everything else so we can't use getExtOrDefault
  def kotlin_version = rootProject.ext.has('kotlinVersion') ? rootProject.ext.get('kotlinVersion') : project.properties['StripeIdentityReactNative_kotlinVersion']

  repositories {
    mavenLocal()
    google()
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:8.5.2'
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['StripeIdentityReactNative_' + name]
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties['StripeIdentityReactNative_' + name]).toInteger()
}

android {
  namespace "com.stripeidentityreactnative"
  compileSdkVersion getExtOrIntegerDefault('compileSdkVersion')
  defaultConfig {
    minSdkVersion getExtOrIntegerDefault('minSdkVersion')
    targetSdkVersion getExtOrIntegerDefault('targetSdkVersion')
    versionCode 1
    versionName "1.0"

  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }
  lintOptions {
    disable 'GradleCompatible'
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }
}

repositories {
  mavenLocal()
  google()
  mavenCentral()
}

def kotlin_version = getExtOrDefault('kotlinVersion')
def stripe_version = getExtOrDefault('stripeVersion')

dependencies {
  // Lock to React Native 0.73.9 for compatibility
  api 'com.facebook.react:react-android:0.73.9'
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  implementation("com.stripe:identity:$stripe_version") {
    exclude group: 'androidx.emoji2', module: 'emoji2'
  }
  implementation('androidx.emoji2:emoji2:1.3.0').force // Avoid using 1.4.0 since that requires targetSdkVersion 34
}
